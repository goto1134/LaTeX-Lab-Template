GOAL:	RATING

INITIAL:
	form main
	at 10, 25 put "*******************************";
	at 11, 25 put "*        EXPERT SYSTEM        *";
	at 12, 25 put "*   SOCIAL TRADING PLATFORM   *";
	at 13, 25 put "******** press any key ********";
	endform

	e.trac = <n>
	e.rigr = <c>
	e.tryp = <s>
	e.cfva = <pp>
	e.lnum = 3
	e.deci = 0

	RATING= UNKNOWN;
	STUDYING= UNKNOWN;
	WALLET= UNKNOWN;
	NUMOFCURRENCIES= UNKNOWN;
	ADVISERS= UNKNOWN;
	USEREALMONEY= UNKNOWN;
	PERSENTOFRISKY= UNKNOWN;
	DEMOACCOUNT= UNKNOWN;
	HASSTUDYMATERIALS= UNKNOWN;
	MINSTART= UNKNOWN;
	
	clear;
	putform main;
	wait;
	clear;

DO:

	? "-------------------Result-------------"

	if hival(RATING)
		? "STP is good. Garantee is ", hicf(RATING);
	else
		? "STP is bad. Garantee is ", hicf(RATING);
	endif;

var:	RATING
	type:	PP

var: STUDYING
	type: pp

rule: BADSTUDYING
	PRIORITY: 80
	if: hival(STUDYING) = false
	then: RATING-=true cf hicf(STUDYING)/2
	reason: If one can not study well, it is hard to start trading

rule: GOODSTUDYING
	PRIORITY: 80
	if: hival(STUDYING) = true
	then: RATING+=true cf hicf(STUDYING)/2
	reason: If one can study well, it is easy to start trading

var: DEMOACCOUNT
	find:
		input tmpv num with "Is there a demo account: (1-No, 2-Free, 3-Paid)"
		input tmpv2 num with "Sure for, %: (0-100)"
		if tmpv=1 then
			let DEMOACCOUNT = "no" cf tmpv2;
		else if tmpv=2 then
				let DEMOACCOUNT = "free" cf tmpv2;
		else
				let DEMOACCOUNT = "paid" cf tmpv2;
		endif;endif;

rule:	NODEMO
	PRIORITY: 80
	if:	DEMOACCOUNT = "no"
	then:	STUDYING += false cf hicf(DEMOACCOUNT)
	reason: If there is no demo account it is very hard to study

rule:	FREEDEMO
	PRIORITY: 80
	if:	DEMOACCOUNT = "free"
	then:	STUDYING += true cf hicf(DEMOACCOUNT)
	reason: If demo account is free it is easyer to do first steps

rule:	PAIDDEMO
	PRIORITY: 80
	if:	DEMOACCOUNT = "paid"
	then:	STUDYING += true cf hicf(DEMOACCOUNT)*3/4
	reason: If demo account is paid it is harder to start studying

var: HASSTUDYMATERIALS
	find:	input tmpv num with "Are there Study Materials: (1-No, 2-Yes)"
		input tmpv2 num with "Sure for, %: (0-100)"
		if tmpv=1 then
			let HASSTUDYMATERIALS = false cf tmpv2;
		else
			let HASSTUDYMATERIALS = true cf tmpv2;
		endif;
	label:	has study materials

rule:	HASSTUDYMATERIALS
	PRIORITY: 80
	if:	HASSTUDYMATERIALS = true
	then:	STUDYING += true cf hicf(HASSTUDYMATERIALS)*3/4
	reason: If there are materials it is easyer to study

rule:	NOSTUDYMATERIALS
	PRIORITY: 80
	if:	HASSTUDYMATERIALS = false
	then:	STUDYING += false cf hicf(HASSTUDYMATERIALS)*3/4
	reason: If there are no study materials its harder to search for information

var: WALLET
	type: PP

rule: BADWALLET
	PRIORITY: 80
	if: hival(WALLET) = false
	then: RATING+=false cf hicf(WALLET)/2
	reason: If the wallet is bad it is more risky to trade

rule: GOODWALLET
	PRIORITY: 80
	if: hival(WALLET) = true
	then: RATING+=true cf hicf(WALLET)/2
	reason: If the wallet is good it is less risky to trade

var: MINSTART
	find:	input tmpv num with "Min start capital (1-Little, 2-Medium, 3-Many)"
		input tmpv2 num with "Sure for, %: (0-100)"
		if tmpv=1 then
			let MINSTART = "little" cf tmpv2;
		else if tmpv=2 then
				let MINSTART = "medium" cf tmpv2;
		else
				let MINSTART = "many" cf tmpv2;
		endif;endif;

rule: LITTLEMIN
	PRIORITY: 80
	if: MINSTART = "little"
	then: WALLET+=true cf hicf(MINSTART)*3/4
	reason: If little money needed to start the easier it is to invite investors

rule: MEDIUMMIN
	PRIORITY: 80
	if: MINSTART = "medium"
	then: WALLET+=true cf hicf(MINSTART)/2
	reason: If medium money needed to start it is harder to invite investors

rule: LARGEMIN
	PRIORITY: 80
	if: MINSTART = "many"
	then: WALLET+=false cf hicf(MINSTART)*3/4
	reason: If many money needed to start it is tears away investors

var: NUMOFCURRENCIES
	find: input tmpv num with "Number of currencies (1-little, 2-many)"
		input tmpv2 num with "Sure for, %: (0-100)"
		if tmpv = 1
			let NUMOFCURRENCIES = "little" cf tmpv2;
		else
			let NUMOFCURRENCIES = "many" cf tmpv2;
		endif;

rule: LITTLECURRENCIES
	PRIORITY: 80
	if: NUMOFCURRENCIES = "little"
	then: WALLET+=false cf hicf(NUMOFCURRENCIES)
	reason: the less currencies you can trade the less opportunities to invest

rule: MANYCURRENCIES
	PRIORITY: 80
	if: NUMOFCURRENCIES = "many"
	then: WALLET+=true cf hicf(NUMOFCURRENCIES)
	reason: the more currencies you can trade the more opportunities to invest

var: ADVISERS
	type: PP

rule: BADADVISERS
	PRIORITY: 80
	if: hival(ADVISERS) = false
	then: RATING+=false cf hicf(ADVISERS)*3/4
	reason: If the advisers are bad there is nothing to do here

rule: GOODADVISERS
	PRIORITY: 80
	if: hival(ADVISERS) = true
	then: RATING+=true cf hicf(ADVISERS)*3/4
	reason: If the advisers are bad the platform is ok

var: USEREALMONEY
	find: input tmpv num with "Must advisers use real money? (1-Yes, 2-No)"
		input tmpv2 num with "Sure for, %: (0-100)"
		if tmpv = 1
			let USEREALMONEY = true cf tmpv2;
		else
			let USEREALMONEY = false cf tmpv2;
		endif;

rule:	MUSTUSEREALMONEY
	PRIORITY: 80
	if:	USEREALMONEY = true
	then: ADVISERS += true cf hicf(USEREALMONEY)*2/3
	reason: If the advisers use their real money they are more careful

rule:	CANUSEDEMOMONEY
	PRIORITY: 80
	if:	USEREALMONEY = false
	then:	ADVISERS += false cf hicf(USEREALMONEY)*2/3
	reason: If the advisers use their real money they are less careful

var: PERSENTOFRISKY
	find: input tmpv num with "What is persent of risky advisors? (1-100)"
		input tmpv2 num with "Sure for, %: (0-100)"
		let PERSENTOFRISKY=tmpv cf tmpv2;

rule:	RISKYADVISERS
	PRIORITY: 80
	if:	PERSENTOFRISKY>70
	then:	ADVISERS += false cf hicf(PERSENTOFRISKY)
	reason: If too many advisers are risky the followers have less oportunities to divercification

rule:	NOTRISKYADVISERS
	PRIORITY: 80
	if:	PERSENTOFRISKY<=70
	then:	ADVISERS += true cf hicf(PERSENTOFRISKY)
	reason: If there are many stable advisers the followers money are safer

END:
